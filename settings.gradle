pluginManagement {
    gradle.ext.kotlinVersion = '1.4.20'
    gradle.ext.agpVersion = '4.2.2'

    plugins {
        id "org.jetbrains.kotlin.android" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.jvm" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.kapt" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.serialization" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.parcelize" version gradle.ext.kotlinVersion
        id "com.android.application" version gradle.ext.agpVersion
        id "com.android.library" version gradle.ext.agpVersion
        id "io.sentry.android.gradle" version "2.0.0"
        id "com.automattic.android.publish-to-s3" version "0.6.1"
        id "se.bjurr.violations.violation-comments-to-github-gradle-plugin" version "1.67"
        id "com.automattic.android.fetchstyle" version "1.1"
    }
    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android'
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            // TODO: Remove this as soon as fetchstyle starts supporting Plugin Marker Artifacts
            if (requested.id.id == "com.automattic.android.fetchstyle") {
                useModule("com.automattic.android:fetchstyle:1.1")
            }
        }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.4.1"
}

include ':WordPress'

include ':libs:image-editor:ImageEditor'
include ':libs:networking:WordPressNetworking'
include ':libs:analytics:WordPressAnalytics'
include ':libs:editor:WordPressEditor'
include ':libs:WordPressProcessors'
include ':libs:WordPressAnnotations'

include ':WordPressMocks'
project(':WordPressMocks').projectDir = new File(rootProject.projectDir, properties.getOrDefault('wp.wordpress_mocks_path', 'libs/mocks') + '/WordPressMocks')

gradle.ext.fluxCBinaryPath = "org.wordpress:fluxc"
gradle.ext.wputilsBinaryPath = "org.wordpress:utils"
gradle.ext.gutenbergMobileBinaryPath = "org.wordpress-mobile.gutenberg-mobile:react-native-gutenberg-bridge"
gradle.ext.loginFlowBinaryPath = "org.wordpress:login"
gradle.ext.includedBuildGutenbergMobilePath = null

def localBuilds = new File('local-builds.gradle')
if (localBuilds.exists()) {
    apply from: localBuilds

    /*
    In order to add a new included build:
    1. Define a property for its path in the `local-builds.gradle-example` file.
    2. Check if that property exists in `ext` since it may be commented out.
    3. Include the build using the property.
    4. Add dependencySubstitution block and substitute the binary module with the project.

    Note that `project` dependencies are resolved in the context of the included build.
    https://docs.gradle.org/current/javadoc/org/gradle/api/initialization/ConfigurableIncludedBuild.html#dependencySubstitution-org.gradle.api.Action-
    */

    if (ext.has("localFluxCPath")) {
        includeBuild(ext.localFluxCPath) {
            dependencySubstitution {
                println "Substituting fluxc with the local build"
                substitute module("$gradle.ext.fluxCBinaryPath") with project(':fluxc')
            }
        }
    }

    if (ext.has("localWPUtilsPath")) {
        includeBuild(ext.localWPUtilsPath) {
            dependencySubstitution {
                println "Substituting wputils with the local build"
                substitute module("$gradle.ext.wputilsBinaryPath") with project(':WordPressUtils')
            }
        }
    }

    if (ext.has("localGutenbergMobilePath")) {
        gradle.ext.includedBuildGutenbergMobilePath = ext.localGutenbergMobilePath
        includeBuild("$ext.localGutenbergMobilePath/gutenberg/packages/react-native-bridge/android") {
            dependencySubstitution {
                println "Substituting gutenberg-mobile with the local build"
                substitute module("$gradle.ext.gutenbergMobileBinaryPath") with project(':react-native-bridge')
            }
        }
    }

    if (ext.has("localLoginFlowPath")) {
        includeBuild(ext.localLoginFlowPath) {
            dependencySubstitution {
                println "Substituting login-flow with the local build"
                substitute module("$gradle.ext.loginFlowBinaryPath") with project(':WordPressLoginFlow')
            }
        }
    }
}

// Build cache is only enabled for CI, at least for now
if (System.getenv().containsKey("CI")) {
    buildCache {
        remote(HttpBuildCache) {
            url = "http://10.0.2.215:5071/cache/"
            allowUntrustedServer = true
            allowInsecureProtocol = true
            push = true
        }
    }
}
